apply plugin: 'com.android.application'

android {
  // app can use the API features included in this API level and lower.
  compileSdkVersion 30

  // can override some attributes in main/AndroidManifest.xml
  defaultConfig {
    applicationId 'net.sourceforge.smallbasic'
    minSdkVersion 16
    targetSdkVersion 30
    versionCode 44
    versionName "12.22"
    resConfigs "en"
  }

  signingConfigs {
    release {
      Properties properties = new Properties()
      properties.load(project.rootProject.file('local.properties').newDataInputStream())
      storeFile file(properties.getProperty('storeFile'))
      keyAlias properties.getProperty('keyAlias')
      storePassword properties.getProperty('storePassword')
      keyPassword properties.getProperty('keyPassword')
    }
  }

  buildTypes {
    debug {
      buildConfigField "boolean", "DEBUG_VIEW", "true"
      buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"
    }
    release {
      ndk {
       debugSymbolLevel = 'FULL'
      }
      buildConfigField "boolean", "DEBUG_VIEW", "false"
      buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"
      shrinkResources true
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
  }
  externalNativeBuild {
    ndkBuild {
      path '../jni/Android.mk'
    }
  }
}

dependencies {
  implementation 'androidx.core:core:1.3.2'
  testImplementation 'junit:junit:4.13.1'
}
